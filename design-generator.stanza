#use-added-syntax(jitx)
defpackage design-template :
  import core
  import jitx
  import jitx/commands
  import ocdb/utils/generic-components
  import ocdb/utils/generator-utils
  import ocdb/utils/bundles

; =========================
; Module to run as a design
; ==========================
val board-shape = NAYN/outline

public pcb-module led-driver (address:Int):
  pin vdd
  pin gnd
  port i2c : i2c

  public inst driver : components/IS31FL3236A-QFLS2-TR/component
  bypass-cap-strap(driver.VCC, gnd, 2.2e-6)
  net (driver.VCC vdd)
  net (driver.GND[0] driver.GND[1] driver.EPAD gnd)
  net (vdd driver.SDB)
  net (i2c.sda driver.SDA)
  net (i2c.scl driver.SCL)
  res-strap(driver.R-EXT gnd, 3.3e3)

  switch(address) :
    0 : 
      net (driver.AD gnd)
    1 : 
      net (driver.AD driver.SCL)
    2 : 
      net (driver.AD driver.SDA)
    3 : 
      net (driver.AD driver.vdd)
    else :
      fatal(to-string("Unsupported I2C address: %_ on IS31FL3236A" % [address]))

  schematic-group(self) = led-driver
  layout-group(self) = led-driver

pcb-module taste-the-rainbow (rows:Int, columns:Int, x-pitch:Double, y-pitch:Double):
  port rgba : rgb-led[rows][columns]

  inst led : database-part(["manufacturer" => "Foshan NationStar Optoelectronics", "mpn" => "NH-Z1921RGBA-SF"])[rows][columns]
  ; net (led[0][0].DIN input)
  defn rainbow-pose (row:Int, column:Int) -> Pose:
    val diff = math/sin((-3.0 * 3.14159 * to-double(column)) / 15.0) * 5.0
    loc(to-double(column) * x-pitch, diff + to-double(row) * y-pitch)

  for c in 0 to columns do :
    for r in 0 to rows do :
      net (led[r][c].R rgba[r][c].r)
      net (led[r][c].G rgba[r][c].g)
      net (led[r][c].B rgba[r][c].b)
      net (led[r][c].p[1] rgba[r][c].a)
      place(led[r][c]) at rainbow-pose(r, c) on Top

pcb-module my-design :
  inst NAYN : NAYN/nyancat
  place(NAYN) at loc(0.0, 0.0) on Top
   
  val rows = 8
  val columns = 12
  inst rainbow : taste-the-rainbow(rows, columns, 4.5 , 7.0)
  place(rainbow) at loc(-65.0, -25.0) on Top


  net led-a-ctrl:pin[8] ()
  for drv in 0 to 2 do :
    inst drive : led-driver(drv)
    require ctrl-bank : gpio[36] from drive.driver

    for j in 0 to 4 do :
      for k in 0 to columns do :
        net (rainbow.rgba[j * drv][k].r ctrl-bank[k * 3].gpio)
        net (rainbow.rgba[j * drv][k].g ctrl-bank[1 + k * 3].gpio)
        net (rainbow.rgba[j * drv][k].b ctrl-bank[2 + k * 3].gpio)
        net (rainbow.rgba[j * drv][k].a led-a-ctrl[j * drv])

  inst amp : main/class-d-audio

  inst usb : usb2-on-a-usb-c-connector(ocdb/components/korean-hroparts-elec/TYPE-C-31-M-23/module(SinkDefault))

  net GND (usb.usb-2.vbus.gnd)
  symbol(GND) = ocdb/utils/symbols/ground-sym
  property(GND.voltage) = typ(0.0)

  ; Power - battery
  inst battery : database-part(["vendor_part_numbers.lcsc" => "C47647"])
  net (battery.p[1] GND)

  ; battery charging
  inst charger : components/BQ24078RGTR/module
  net VBAT (battery.p[2] charger.bat+)
  net (charger.gnd GND)

  ; power switch
  inst sw : database-part(["manufacturer" => "G-Switch", "mpn" => "MK-12C02-G025"])
  net SW-VOUT (sw.p[2] charger.vout)
  net P4V2 (sw.p[3])
  property(P4V2.voltage) = min-max(3.5 5.0)
  net VBUS (charger.vin usb.usb-2.vbus.vdd)

  ; power - LDO
  inst ld : components/AP2125K-2_8TRG1/module
  net (P4V2 ld.vin ld.en)
  net (ld.gnd, GND)
  net P2V8 (ld.vout)

  ; set net symbols
  symbol(VBAT) = ocdb/utils/symbols/supply-sym
  symbol(VBUS) = ocdb/utils/symbols/supply-sym
  symbol(P4V2) = ocdb/utils/symbols/supply-sym
  symbol(P2V8) = ocdb/utils/symbols/supply-sym

  ; RP2040
  inst rpi : components/RP2040/module
  net (GND rpi.gnd)
  net (usb.usb-2.data rpi.usb)
  schematic-group(rpi) = rpi
  layout-group(rpi) = rpi
  net (rpi.vdd P2V8)


; ========================================
; Configure the design and view the result
; ========================================
defn run-design (circuit:Instantiable, run-checks?:True|False) :
  ; set-paper(ANSI-A5)
  set-current-design("hackathon-2022")
  set-board(ocdb/utils/defaults/default-board(ocdb/manufacturers/stackups/jlcpcb-jlc2313, board-shape))
  set-rules(ocdb/manufacturers/rules/jlcpcb-rules)
  var main-module = circuit

  if run-checks? :
    main-module = ocdb/utils/generator-utils/run-final-passes(circuit) ; Analyze design with a pass
  
  set-main-module(main-module) ; Treat the provided module as a design, and compile it.
  set-use-layout-groups()
  set-paper(ANSI-A)
  if run-checks? :
    run-checks("checks.txt")
    view-design-explorer()
    export-design()
  else:  
    view-board()
    view-schematic()
    view-design-explorer()

; ====================
; Export design to CAD
; ====================
defn export-design () :
  set-paper(ANSI-A4)
  set-export-backend(`kicad)
  export-cad()

; ===================================
; Update CAD, keeping layout progress
; ===================================
defn update-design () :
  set-export-board?(false)
  export-design()

; ============
; Run the code
; ============
run-design(my-design, false)